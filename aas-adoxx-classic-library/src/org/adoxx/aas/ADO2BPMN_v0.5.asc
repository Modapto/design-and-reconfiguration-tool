PROCEDURE global GET_BPMN modelid:string bpmn:reference {
  CC "AdoScript" GET_TEMP_FILENAME
  SETL sXSLTfilePath:(filename + ".xsl")
  SETL sTempFilePath:(filename + ".xml")
  SETL sBPMNFile:(filename + ".bpmn")
  
  CC "AdoScript" FILE_COPY from:("db:\\ADO2BPMN_v0.5.xsl") to:(sXSLTfilePath)
  CC "Documentation" XML_MODELS modelids:(modelid)
  SETL sNewXML:(replace(xml, "xmlns=\"@boc-eu.com/boc-is/adonis.model.document;1\" xmlns:xsi=\"http://www.w3.org/2001/XMLSchema-instance\" xsi:schemaLocation=\"@boc-eu.com/boc-is/adonis.model.document;1 adoxmlmodel.xsd\"", ""))
  #CC "AdoScript" VIEWBOX text:(sNewXML)
  CC "AdoScript" FWRITE file:(sTempFilePath) text:(sNewXML) append:0 binary:0 base64:0
  #SETL sTransformationCommand: ( "java -jar tools\\saxonb8j\\saxon8.jar -t -o \"" + sBPMNFile + "\" \"" + sTempFilePath + "\" \"" + sXSLTfilePath+ "\"")
  SETL sTransformationCommand:("tools\\xsltproc\\xsltproc.exe -o \"" + sBPMNFile + "\" \"" + sXSLTfilePath+ "\" \"" + sTempFilePath + "\"")
  #CC "AdoScript" INFOBOX (sTransformationCommand)
  #SYSTEM ("cmd /c " + sTransformationCommand) with-console-window
  SYSTEM (sTransformationCommand) with-console-window
  CC "AdoScript" FREAD file:(sBPMNFile)
  SETL bpmn:(text)
  CC "AdoScript" FILE_DELETE file:(sTempFilePath)
  CC "AdoScript" FILE_DELETE file:(sXSLTfilePath)
  CC "AdoScript" FILE_DELETE file:(sBPMNFile)
}

PROCEDURE global EXPORT_MODEL_AS_BPMN {
  #CC "CoreUI" MODEL_SELECT_BOX oktext:("Export") boxtext:("Select BPMN to export") title:("Export to BPMN")
  CC "ImportExport" SHOW_EXPORT_DLG multi-select:0 title:"Select the BPMN Models to export" filedescription:"bpmn2 format" fileextension:"*.bpmn"
  IF (endbutton != "ok") {
    EXIT
  }
  SETL sExportPath:(filename)
  CC "Core" GET_MODEL_INFO modelid:(VAL modelids)
  IF (NOT (modeltype = "Business process diagram (BPMN 2.0)" OR modeltype = "Business Process Diagram (BPMN 2.0)")) {
    CC "AdoScript" ERRORBOX ("The BPMN export is available only for 'Business process diagram (BPMN 2.0)' model type. Selected model type: " + modeltype)
    EXIT
  }
  GET_BPMN modelid:(modelids) bpmn:bpmn
  #CC "AdoScript" VIEWBOX text:(bpmn)
  IF (bpmn != "") {
    CC "AdoScript" FWRITE file:(sExportPath) text:(bpmn) append:0 binary:0 base64:0
    CC "AdoScript" INFOBOX "Export Completed"
  } ELSE {
    CC "AdoScript" ERRORBOX ("Error during the BPMN generation")
  }
  
}
#EXPORT_MODEL_AS_BPMN